plugins {
    id 'com.android.application'
}

android {
    compileSdk 34
    buildToolsVersion "34.0.0"

    defaultConfig {
        applicationId "io.github.lsposed.disableflagsecure"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 6
        versionName "3.0.0"
    }

    Properties localProperties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    signingConfigs {
        if (localProperties.getProperty("storeFile") != null) {
            config {
                storeFile file(localProperties.getProperty("storeFile"))
                storePassword localProperties.getProperty("storePassword")
                keyAlias localProperties.getProperty("keyAlias")
                keyPassword localProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            if (localProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.config
            }
        }
        release {
            if (localProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.config
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lint {
        checkReleaseBuilds false
    }

    dependenciesInfo.includeInApk false
    namespace 'io.github.lsposed.disableflagsecure'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
}
